block(`rv_context) {
  alias_all(`traces:traces),
  alias_all(`checkers:util),
  export(`{
    RVContexts(c, r) -> CallEvent(c), Range(r).
    Frequency[f, r] = n -> string(f), Range(r), int(n).
    Total[f] = n -> string(f), int(n).
    Majority(f, r) -> string(f), Range(r).
    BugFree(c) -> CallEvent(c).
    UsedLater(c) -> Event(c).
    Minority(c) -> CallEvent(c).
    Buggy(c) -> CallEvent(c).
  }),

  clauses(`{
    RVContexts(c, r) <- Call[id] = c,
                        Follows(c, check),
                        Checks(check, e, r),
                        In(id, e).

    Frequency[f, r] = n <- 
      agg<<n = count()>> RVContexts(c, r), CallName[c] = f.

    Total[f] = n <- agg<<n = count()>> RVContexts(c, _), CallName[c] = f.

    Majority(f, r) <- Frequency[f, r] = freq, Total[f] = tot, int:decimal:convert[freq] / int:decimal:convert[tot] > 0.3.
    
    BugFree(c) <- RVContexts(c, r), Majority(f, r), CallName[c] = f.
    UsedLater(c) <- Follows(c, _).
    Minority(c) <- !BugFree(c), Majority(f, _), CallName[c] = f.
    Buggy(c) <- Minority(c), UsedLater(c).
  })
} <-- .
